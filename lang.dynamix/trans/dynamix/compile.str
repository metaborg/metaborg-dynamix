module compile

imports
  codegen/dnx-compile
  codegen/dnx-loader
  codegen/fvm-primitive
  
  fvm-util
  fvm-common
  codegen/dnx-common
  
rules // Debugging
  // Compile an AST to FVM bytecode using the loaded Dynamix spec 
  dnx-compile-to-fvm:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"dnx.rgr")> path;
      result := <compile-file-to-fvm; fvm-pp-flat> (path, ast)
  
  
  // Compile an AST to FVM bytecode using the loaded Dynamix spec,
  // Store it in a file and execute the file
  dnx-run-fvm:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      filename  := <guarantee-extension(|"dnx.value")> path;
      compiled  := <compile-file-to-fvm> (path, ast);
      (_, file) := <store-fvm-program-to-file> (project-path, filename, compiled);
      result    := <eval-framevm(|<remove-extension> file)> compiled
  
  // Compile an AST to FVM bytecode using the loaded Dynamix spec,
  // Store it in a file and execute the file in debug mode
  dnx-run-fvm-debug:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      filename  := <guarantee-extension(|"dnx.value")> path;
      compiled  := <compile-file-to-fvm> (path, ast);
      (_, file) := <store-fvm-program-to-file> (project-path, filename, compiled);
      result    := <eval-framevm-debug(|<remove-extension> file)> compiled
  
  // Compile an AST to FVM bytecode using the loaded Dynamix spec,
  // The AST is shown as Aterm and not pretty-printed
  dnx-compile-to-flat-ast:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"dnx.aterm")> path
    ; fname    := <base-filename; remove-extension> path
    ; compiled_spec := <dnx-load-compiled-spec>
    ; result   := <dnx-compile> (ast, compiled_spec)
  
  // Show the loaded DNX specification Aterm
  dnx-show-spec:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"dnx.aterm")> path
    ; fname    := <base-filename; remove-extension> path
    ; result := <dnx-load-compiled-spec>
    
rules
  // Store a FVM AST to the specified file
  store-fvm-program-to-file: (path, basename, ast) -> (path, filename)
    with
      filename := <guarantee-extension(|"rgr")> basename;
      <WriteToFile(fprint)> (filename, [<fvm-pp-flat> ast])

  // Compile a file with given AST to FVM bytecode.
  // First load the active DNX spec and use this to compile the AST
  compile-file-to-fvm: (path, ast) -> result
    with
      fname    := <base-filename; remove-extension> path
    ; compiled_spec := <dnx-load-compiled-spec>
    ; prog := <dnx-compile> (ast, compiled_spec)
    ; result := <((?( (header, instr) ); !(header, [(<dnx-primitive-mk-label> "MAIN", FVM_AutoSize()) | instr]) ) <+ id); fvm-compile-flat>
  
  // Convert the compile output to either a valid FVM AST or a string of the encountered errors
  fvm-compile-flat: prog@(_, _) -> <rgr-from-flat; rgr-assign-registers> prog
  fvm-compile-flat: errors -> <map( (\ DNX_Error(e) -> <concat-strings> [e, "\n"] \) ); concat-strings> errors
  
  // Prettyprint the FVM AST or propagate the errors
  fvm-pp-flat: errors -> errors where is-string
  fvm-pp-flat: prog -> <pp-framevm> prog
