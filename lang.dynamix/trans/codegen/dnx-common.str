module dnx-common

imports
  signatures/Dynamix-sig

signature
  sorts S
  constructors    
    Module : STRING * [Import] * [STRING] * [Def] * [Def] * Header -> S
    DNX_Header: [Label] * [Continuation] * [Namespace] -> S
    NormModule: [STRING] * [STRING] * [NormDef] -> S
    DefGroup: STRING * [DefCase] -> S
    DefCase: [Pattern] * [STRING] * [instruction] -> S
    DNX_BindExp: [STRING] * [Exp] -> S
    MatchError: STRING -> S

     
rules
    new-static: (name, val) -> val
      with
        <set-counter> (name, val)
        
    get-static: name -> val 
      where
        val := <get-counter> name;
        <set-counter> (name, val)
        
    update-static: (name, val) -> val 
      where
        <set-counter> (name, val)
        
    find-index(s): [] -> <fail> 
    find-index(s): [name|_] -> 0 where <s> name
    find-index(s): [h|t] -> <find-index(s); inc> t
    
    update-map: ([], key, val) -> <fail>
    update-map: ([(key, _) | t], key, val) -> [(key, val) | t]
    update-map: ([ h | t], key, val) -> [ h | <update-map> (t, key, val)]
    
    split-list3(s1, s2, s3): [] -> ([], [], [])
    split-list3(s1, s2, s3): [h | t] -> ([h|a], b, c) where <s1> h;
      (a, b, c) := <split-list3(s1, s2, s3)> t
    split-list3(s1, s2, s3): [h | t] -> (a, [h|b], c) where <s2> h;
      (a, b, c) := <split-list3(s1, s2, s3)> t
    split-list3(s1, s2, s3): [h | t] -> (a, b, [h|c]) where <s3> h;
      (a, b, c) := <split-list3(s1, s2, s3)> t
    
    