module analysis/static-semantics

imports

  signatures/-
  analysis/definitions
  analysis/common

rules

  init ^ (s) := new s.
  [[mod@Module(name@"dynamic-semantics", _, _, _, _, DNX_Header([], _, _)) ^(s)]] :=
    false | error $[Missing 'link-labels' in signature] @name,
    module[[mod ^(s)]].

  [[mod@Module(name@"dynamic-semantics", _, _, _, _, DNX_Header(_, [], _)) ^(s)]] :=
    false | error $[Missing 'continuations' in signature] @name,
    module[[mod ^(s)]].

  [[mod@Module(name@"dynamic-semantics", _, _, _, _, DNX_Header(_, _, [])) ^(s)]] :=
    false | error $[Missing 'namespaces' in signature] @name,
    module[[mod ^(s)]].

  [[mod@Module("dynamic-semantics", _, _, _, _, _) ^(s)]] :=
    module[[mod ^(s)]].

  [[mod@Module(name, _, _, _, _, DNX_Header([_|_], [_|_], [_|_])) ^(s)]] :=
    false | error $[Signature only allowed in the main file (for now) ] @name,
    module[[mod ^(s)]].

  [[mod@Module(_, _, _, _, _, _) ^(s)]] :=
    module[[mod ^(s)]].
  
  module[[Module(name, imports, prim_names, Defs(types), Defs(defs), DNX_Header(links, continuations, namespaces)) ^(s)]] :=
    new module, module ---> s,
    Module{name} <- s,
    Module{name} ===> module, 
    Module{name}.defs := defs,
    distinct/name D(s)/Module | error $[Multiple modules with name [name]] @NAMES,
    
    Map1(import) [[imports ^(module)]],
    initTypes[[ name ^(s)]],
    Map1(initLinks) [[ links ^(s)]],
    Map1(initConts) [[ continuations ^(s)]],
    Map1(defNames) [[prim_names ^(module)]],
    Map1(typeDecl) [[types ^(module)]],
    new defs_s, defs_s ---> module,
    new quotes_s, quotes_s ---> module,
    Map1(def) [[defs ^(quotes_s, defs_s)]].
    
    
  import [[name ^(s)]] :=
    Module{name} -> s,
    Module{name} |-> d | error $[Cannot resolve module '[name]'] @name,
    Module{name} <=== s.

  typeDecl [[TypeDef(name@"eval", paramType, args, ret) ^(s) ]] :=
    Def{name} <- s,
    type [[ret ^(s) : retty]],
    Map1T(type) [[args ^(s) : argty]],
    Map1T(paramTypeDecl) [[paramType ^(s) : paramty]],
    
    Def{name}:DefT(paramty, argty, retty) | error $[Invalid type for rule eval, should be 'ast -> instr'] @name,
    Def{name}:DefT([], [TermT()], InstrT()),
    distinct/name D(s)/Def | error $[Multiple type signatures for rule [name]] @NAMES.
    
  typeDecl [[TypeDef(name, paramType, args, ret) ^(s) ]] :=
    Def{name} <- s,
    type [[ret ^(s) : retty]],
    Map1T(type) [[args ^(s) : argty]],
    Map1T(paramTypeDecl) [[paramType ^(s) : paramty]],
    Def{name}:DefT(paramty, argty, retty)!,
    distinct/name D(s)/Def | error $[Multiple type signatures for rule [name]] @NAMES.
  
  paramTypeDecl [[ParamType(args, ret) ^(s) : ParamT(argty, retty)]] :=
    type [[ret ^(s) : retty]],
    Map1T(type) [[args ^(s) : argty]].
      
  
  initLinks [[(idx, lbl) ^(s) ]] :=
    Link{lbl} <- s,
    Link{lbl}: TermT(),
    Link{lbl}.index := idx.
    
  initConts [[(0, lbl) ^(s) ]] :=
    false | note $[Return of the initial control frame] @lbl,
    Cont{lbl} <- s,
    Cont{lbl}: TermT(),
    Cont{lbl}.index := idx.
    
  initConts [[(1, lbl) ^(s) ]] :=
    false | note $[Exeption handler of the initial control frame] @lbl,
    Cont{lbl} <- s,
    Cont{lbl}: TermT(),
    Cont{lbl}.index := idx.
    
  initConts [[(idx, lbl) ^(s) ]] :=
    Cont{lbl} <- s,
    Cont{lbl}: TermT(),
    Cont{lbl}.index := idx.
    
  def[[Def(name, param, args, Block(instrs)) ^(q, s)]] :=
    Def{name} -> s,
    Def{name} |-> d | error $[No type signature for rule [name]],
    d:DefT(dparamty, dtys, drty),
    new def_s,
    def_s ---> s,
    Map1T(defParam) [[param ^(def_s) : paramty]],
    Map1T(term) [[args ^(def_s) : tys]],
    Fold3T(instr) [[instrs ^(q, def_s, _) : rty]],
    dtys == tys,
    dparamty == paramty,
    rty <sub? drty | error $[Wrong return type. Expected [drty], but got [rty]] @name.
  
  defParam [[name ^(s) : ParamT(argty, rty) ]] :=
    Def{name} <- s,
    Def{name}:DefT([], argty, rty)!.
    
  type[[ Term()   ^(s) : TermT()   ]].
  type[[ Code()   ^(s) : CodeT()   ]].
  type[[ Exp()    ^(s) : ExpT()    ]].
  type[[ Instr()  ^(s) : InstrT()  ]].
  type[[ Return() ^(s) : ReturnT() ]].
  
  
  term[[DNXTerm_Var(name) ^(s) : ty]] :=
    Var{name} <- s,
    Var{name}:ty!.
    
  term[[DNXTerm_List(elems) ^(s) : TermT()]] :=
    termMap [[elems ^(s) : TermT()]].
    
  term[[DNXTerm_Tuple(elems) ^(s) : TermT()]] :=
    termMap [[elems ^(s) : TermT()]].
    
  term[[DNXTerm_At(name, pattern) ^(s) : TermT()]] :=
    Var{name} <- s,
    Var{name}:TermT()!,
    term [[pattern ^(s) : TermT()]].
    
  term[[DNXTerm_Type(pattern, type) ^(s) : TermT()]] :=
    term [[pattern ^(s) : TermT()]].
    
  term[[DNXTerm_ListTail(elems, tail) ^(s) : TermT()]] :=
    termMap [[elems ^(s) : TermT()]],
    term [[tail ^(s) : TermT()]].
    
  term[[DNXTerm_ListLast(init, last) ^(s) : TermT()]] :=
    termMap [[last ^(s) : TermT()]],
    term [[init ^(s) : TermT()]].
    
  term[[DNXTerm_Pat(name, args) ^(s) : TermT()]] :=
    termMap [[args ^(s) : TermT()]].
    
  term[[DNXTerm_Int(_) ^(s) : TermT()]].
    
  term[[DNXTerm_String(_) ^(s) : TermT()]].
  term[[DNX_Link(_) ^(s) : TermT()]].
  term[[DNX_Cont(_) ^(s) : TermT()]].
  
  termMap [[ [] ^(s) : ty ]].
  termMap [[ [h|t] ^(s) : ty ]] :=
    term [[h ^(s) : ty]], termMap [[t ^(s) : ty ]].
  
  Fold3T(X) [[ [DNX_Return(arg)] ^(q, s, qp) : ExpT() ]] :=
    rty == ExpT() | error $[A value of type [rty] cannot be returned by a definition],
    exp [[arg ^(q, s, qp) : rty]].
    
  Fold3T(X) [[ [DNX_Return(arg) | tail] ^(q, s, qp) : ExpT() ]] :=
    rty == ExpT() | error $[A value of type [rty] cannot be returned by a definition],
    exp [[arg ^(q, s, qp) : rty]],
    Fold3T(instr) [[tail ^(q, s, qp) : _]],
    false | error $[Dead code] @tail.
    
  Fold3T(X) [[ [h] ^(a, b, c) : ty ]] :=
    X[[ h ^(a, b, c, _, _, _) : ty]].
    
  Fold3T(X) [[ [h | t] ^(a, b, c) : ty ]] :=
    X[[ h ^(a, b, c, a', b', c') : _ ]],
    Fold3T(X)[[ t ^(a', b', c') : ty ]].
  
  
  instr [[ DNX_Bind(name, exp) ^(q, s, qp, q', def_s, qp') : InstrT() ]] :=
    q == q', qp == qp', 
    exp [[exp ^(q, s, qp) : expty]],
    new def_s,
    def_s ---> s,
    Var{name} <- def_s,
    Var{name}:expty!.
    
  instr [[ DNX_Exp(exp) ^(q, s, qp, q', s', qp') : defrty ]] :=
    q == q', s == s', qp == qp', 
    exp[[exp ^(q, s, qp) : defrty]].
    
  instr [[ DNX_Label(_) ^(q, s, qp, q', s', qp') : CodeT() ]] :=
    q == q', s == s', qp == qp'.
  
  exp [[DNX_Link(lbl) ^(q, s, qp) : ty ]] :=
    Link{lbl} -> s,
    Link{lbl} |-> d | error $[Link with label [lbl] is not defined],
    d : ty.
  
  exp [[DNX_Cont(lbl) ^(q, s, qp) : ty ]] :=
    Cont{lbl} -> s,
    Cont{lbl} |-> d | error $[Continuation with label [lbl] is not defined],
    d : ty.
    
  exp [[DNX_Label(_) ^(q, s, qp) : CodeT() ]]. // TODO: Check if these resolve in the current rule
  
  exp [[DNX_Var(name) ^(q, s, qp) : ty ]] :=
    Var{name} -> s,
    Var{name} |-> d | error $[Variable [name] not declared] @name,
    d:ty.
    
  exp [[DNX_Call(name, param, args) ^(q, s, qp) : rty ]] :=
    Def{name} -> s,
    Def{name} |-> d | error $[Rule [name] not declared] @name,
    d:DefT(dparamty, defargtys, defrty)!,
    Map3T(exp) [[args ^(q, s, qp) : argtys]],
    Map1T(expParam) [[param ^(s) : paramty]],
    rty == defrty,
    paramty == dparamty,
    argtys <sub? defargtys | error $[Wrong argument type. Expected [defargtys], found [argtys]].
     
  expParam [[name ^(s) : ParamT(argty, retty) ]] :=
    Def{name} -> s,
    Def{name} |-> d | error $[Rule [name] not defined],
    d:DefT(dparamty, dargty, dretty),
    dparamty == [] | error $[Nesting parameterized rules is not allowed],
    argty == dargty, retty == dretty.
  
  exp [[DNX_Quote(Block(instrs)) ^(q, s, qp) : CodeT()]] :=
    new quote_s, quote_s ---> q,
    qty <sub? InstrT() | error $[Found [qty], expected [InstrT()]],
    Fold3T(instr) [[instrs ^(q, quote_s, s) : qty]].
    
  exp [[DNX_Splice(DNX_Var(name)) ^(q, s, qp) : ty ]] :=
    Var{name} -> qp,
    Var{name} |-> d | error $[Variable [name] not declared] @name,
    d:sty,
    ty == sty.
    
  exp [[DNX_Term(term) ^(q, s, qp) : ty ]] := expterm [[term ^(s) : ty]].
  
  expterm[[DNXTerm_Var(name) ^(s) : ty]] :=
    Var{name} -> s,
    Var{name} |-> d | error $[Variable [name] not defined] @name,
    d:ty!.
    
  expterm[[DNXTerm_List(elems) ^(s) : TermT()]] :=
    exptermMap [[elems ^(s) : TermT()]].
    
  expterm[[DNXTerm_Tuple(elems) ^(s) : TermT()]] :=
    exptermMap [[elems ^(s) : TermT()]].
    
  expterm[[DNXTerm_ListTail(elems, tail) ^(s) : TermT()]] :=
    exptermMap [[elems ^(s) : TermT()]],
    expterm [[tail ^(s) : TermT()]].
    
  expterm[[DNXTerm_ListLast(init, last) ^(s) : TermT()]] :=
    exptermMap [[last ^(s) : TermT()]],
    expterm [[init ^(s) : TermT()]].
    
  expterm[[DNXTerm_Pat(name, args) ^(s) : TermT()]] :=
    exptermMap [[args ^(s) : TermT()]].
    
  expterm[[term@DNXTerm_At(name, pattern) ^(s) : TermT()]] :=
    false | error $[Unsupported operation] @ term.
    
  expterm[[term@DNXTerm_Type(pattern, type) ^(s) : TermT()]] :=
    false | error $[Unsupported operation] @ term.
    
  expterm[[DNXTerm_Int(_) ^(s) : TermT()]].
    
  expterm[[DNXTerm_String(_) ^(s) : TermT()]].
  expterm[[DNX_Link(_) ^(s) : TermT()]].
  expterm[[DNX_Cont(_) ^(s) : TermT()]].
  
  exptermMap [[ [] ^(s) : ty ]].
  exptermMap [[ [h|t] ^(s) : ty ]] :=
    expterm [[h ^(s) : ty]], exptermMap [[t ^(s) : ty ]].
  
    